apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "com.tmack.sermonstream"
        minSdkVersion 21
        targetSdkVersion 23
        versionCode project.build_number as Integer ?: 0
        versionName project.version_number
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    // apply the signing configuration, if provided
    if (project.hasProperty("build_signingConfig")
            && file(project.build_signingConfig).exists()) {
        logger.debug("[SIGNING CONFIG] Applying signing configuration to release builds from: $project.build_signingConfig");
        apply from: project.build_signingConfig, to: android;
    } else {
        logger.debug("[SIGNING CONFIG] No release signing configuration found.");
        logger.debug("[SIGNING CONFIG] $project.build_signingConfig");
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            versionNameSuffix '.atv.release'
        }

        debug {
            minifyEnabled
            signingConfig signingConfigs.debug
            versionNameSuffix '.atv.debug-' + getDate('yyyyMMddHHmm')
        }
    }
}

play {
    serviceAccountEmail = "${System.env.GOOGLE_PLAY_SERVICE_EMAIL}"
    // plugin doesn't allow for setting to null or empty of pk12 file
    pk12File = project.hasProperty('gps_pk_file') ? file(gps_pk_file) : '~/'
    uploadImages = false
    track = project.hasProperty('gps_track') ? gps_track : 'alpha'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:recyclerview-v7:23.0.1'
    compile 'com.android.support:leanback-v17:23.0.1'
    compile 'com.android.support:appcompat-v7:23.0.1'
    compile 'com.android.support:support-v4:23.0.1'
    compile 'com.github.bumptech.glide:glide:3.4.0'
    compile 'com.squareup.picasso:picasso:2.3.2'
    compile files('../libs/volley.jar')
    compile project(':CastCompanionLibrary')
    compile project(':api')
    compile project(':common')

    // testing dependencies
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.9.5'
}
