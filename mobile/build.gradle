apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.tmack.sermonstream"
        minSdkVersion 21
        targetSdkVersion 22
        versionCode project.build_number as Integer ?: 0
        versionName project.version_number
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    // apply the signing configuration, if provided
    if (project.hasProperty("build_signingConfig")
            && file(project.build_signingConfig).exists()) {
        logger.debug("[SIGNING CONFIG] Applying signing configuration to release builds from: $project.build_signingConfig");
        apply from: project.build_signingConfig, to: android;
    } else {
        logger.debug("[SIGNING CONFIG] No release signing configuration found.");
        logger.debug("[SIGNING CONFIG] $project.build_signingConfig");
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            versionNameSuffix '.release'
        }

        debug {
            minifyEnabled
            signingConfig signingConfigs.debug
            versionNameSuffix '.debug-' + getDate('yyyyMMddHHmm')
        }
    }
}

play {
    serviceAccountEmail = "${System.env.GOOGLE_PLAY_SERVICE_EMAIL}"
    // plugin doesn't allow for setting to null or empty of pk12 file
    pk12File = project.hasProperty('gps_pk_file') ? file(gps_pk_file) : '~/'
    uploadImages = true
    track = project.hasProperty('gps_track') ? gps_track : 'alpha'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // android support libraries
    compile 'com.android.support:support-v4:22.2.0'
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:mediarouter-v7:22.2.0'
    // material design elements
    compile 'com.android.support:design:22.2.0'
    compile 'com.android.support:palette-v7:22.2.0'
    compile 'com.android.support:cardview-v7:22.2.0'
    compile 'com.android.support:recyclerview-v7:22.2.0'
    // play service apis
    compile 'com.google.android.gms:play-services-cast:7.5.0'
    compile 'com.google.android.gms:play-services-wearable:7.5.0'
    // include picasso for image loading, maybe switch out for volley later
    compile 'com.squareup.picasso:picasso:2.3.2'
    compile 'com.github.bumptech.glide:glide:3.4.+'
    // include both of Google's libraries GSON and Volley for API integration
    compile 'com.google.code.gson:gson:2.3'
    compile files('../libs/volley.jar')
    // include ShowcaseView
    compile 'com.github.amlcurran.showcaseview:library:5.0.0'
    compile project(':CastCompanionLibrary')
    compile project(':api')
    compile project(':common')
    // testing dependencies
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile 'org.powermock:powermock-module-junit4:1.6.1'
    testCompile 'org.powermock:powermock-api-mockito:1.6.1'
}